program = statement* EOF

statement = print_stmt | let_stmt | assign_stmt | block_stmt | while_stmt
print_stmt = "print" expression ";"
let_stmt = "let" identifier "=" expression ";"
assign_stmt = identifier "=" expression ";"
block_stmt = "{" statement* "}"
while_stmt = "while" expression block_stmt

expression = logic_or
logic_or = logic_and ("||" logic_and)*
logic_and = equality ("&&" equality)*
equality = comparison (("==" | "!=") comparison)*
comparison = term (("<" | ">" | "<=" | ">=") term)*
term = factor (("+" | "-") factor)*
factor = unary (("*" | "/") unary)*
unary = ("!" | "-") unary | primary
primary = identifier | number | bool | string | "nil | "(" expression ")"

identifier = alpha (alpha | digit)*
number = digit+ ("." digit+)?
string = "<anything except ">"
bool = "true" | "false"

alpha = "_" | upper | lower
upper = "A..Z"
lower = "a..z"
digit = "0..9"
